plugins {
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'scala'
apply plugin: 'java-library'

group 'com.sbuslab'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/public' }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'utils'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'utils'
                description = 'Common utils for sbus'
                url = 'https://github.com/sbuslab/utils'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'kulikovd'
                        name = 'Dmitry Kulikov'
                        email = 'kulikov.dm@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/sbuslab/utils'
                    connection = 'scm:https://github.com/sbuslab/utils.git'
                    developerConnection = 'scm:git@github.com:sbuslab/utils.git'
                }
            }
        }
    }

    if (project.findProperty('publishSonatype')) {
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

                credentials {
                    username = project.findProperty('nexusUsername') ?: ''
                    password = project.findProperty('nexusPassword') ?: ''
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    api 'com.sbuslab:sbus:[1.5.0,1.6.0)'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    api 'com.typesafe:config:1.4.2'

    api 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.3'

    api 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.13.3'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.3'
    api 'com.fasterxml.jackson.module:jackson-module-scala_2.12:2.13.3'

    api 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
    api 'org.springframework:spring-jdbc:5.3.20'

    api 'javax.validation:validation-api:2.0.1.Final'
    api 'org.reflections:reflections:0.9.12'

    api 'commons-io:commons-io:2.11.0'
    api 'commons-lang:commons-lang:2.6'
    api 'commons-codec:commons-codec:1.15'

    api 'org.flywaydb:flyway-core:9.4.0'
    api 'javax.transaction:jta:1.1'
    api 'javax.xml.bind:jaxb-api:2.3.1'

    api 'org.postgresql:postgresql:42.4.2'

    api 'org.asynchttpclient:async-http-client:2.12.3'

    api 'org.slf4j:slf4j-api:1.7.36'
    api 'ch.qos.logback:logback-classic:1.2.11'
    runtimeOnly 'net.logstash.logback:logstash-logback-encoder:7.2'

    api 'net.spy:spymemcached:2.12.3'
    api 'io.lettuce:lettuce-core:6.1.8.RELEASE'

    api 'io.prometheus:simpleclient_hotspot:0.15.0'
    api 'io.prometheus:simpleclient_httpserver:0.15.0'
    api 'io.prometheus:simpleclient_logback:0.15.0'

    api 'org.bouncycastle:bcprov-jdk15to18:1.71'
    implementation 'net.i2p.crypto:eddsa:0.3.0'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.33.2'
    testImplementation 'io.qala.datagen:qala-datagen:2.5.1'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.scalatest:scalatest_2.12:3.0.5'
    testImplementation 'org.springframework.boot:spring-boot-test:2.7.0'
    testImplementation 'org.springframework:spring-test:5.3.20'

    constraints {
        api('org.eclipse.jetty:jetty-bom:9.4.48.v20220622') {
            because 'https://security.snyk.io/vuln/SNYK-JAVA-ORGECLIPSEJETTY-2945454 com.github.tomakehurst:wiremock-jre8:2.33.2'
        }
        api('org.yaml:snakeyaml:1.31') {
            because 'https://security.snyk.io/vuln/SNYK-JAVA-ORGYAML-2806360 com.github.tomakehurst:wiremock-jre8:2.33.2'
        }
        api('io.netty:netty-codec:4.1.71.Final') {
            because 'https://security.snyk.io/vuln/SNYK-JAVA-IONETTY-2314893 org.asynchttpclient:async-http-client@2.12.3'
        }
    }
}
